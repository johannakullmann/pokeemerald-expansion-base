const LOCALID_RUTH = 1
const LOCALID_LITTLE_GIRL = 2
const LOCALID_EEVEE = 3
const TEMP_FLAG_HIDE_RUTH = FLAG_TEMP_10
const TEMP_FLAG_HIDE_EEVEE = FLAG_TEMP_11
const TEMP_VAR_PREVENT_NORTH_EXIT_TRIGGER_ACTIVE = VAR_TEMP_3

mapscripts SandyshoreTown_MapScripts {

    MAP_SCRIPT_ON_TRANSITION {
        goto(SandyshoreTown_EventScript_OnTransition)
    }
}

script SandyshoreTown_EventScript_InnSign {
    lock
    msgbox("Sandyshore Inn")
    release
    end
}

script SandyshoreTown_EventScript_PreventExit_Route135 {
    lock
    applymovement(LOCALID_LITTLE_GIRL, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_LITTLE_GIRL, Common_Movement_ExclamationMarkAndDelay)
    waitmovement(0)
    getplayerxy(VAR_TEMP_1, VAR_TEMP_2)
    if (var(VAR_TEMP_1) == 19) {
        applymovement(LOCALID_LITTLE_GIRL, Common_Movement_WalkLeft2)
        delay(16)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
        waitmovement(0)
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
        turnobject(LOCALID_LITTLE_GIRL, DIR_SOUTH)
        msgbox(SandyshoreTown_Text_LittleGirlPreventExit)
        closemessage
        applymovement(LOCALID_LITTLE_GIRL, Common_Movement_WalkRight2)
    }
    elif (var(VAR_TEMP_1) == 20) {
        applymovement(LOCALID_LITTLE_GIRL, Common_Movement_WalkLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, Common_Movement_WalkDown)
        waitmovement(0)
        turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
        turnobject(LOCALID_LITTLE_GIRL, DIR_SOUTH)
        speakername("Minnie")
        msgbox(SandyshoreTown_Text_LittleGirlPreventExit)
        closemessage
        applymovement(LOCALID_LITTLE_GIRL, Common_Movement_WalkRight)
    }
    waitmovement(0)
    release
    end
}

script SandyshoreTown_EventScript_LittleGirl {
    lock
    faceplayer
    speakername("Minnie")
    msgbox(format("I wanna go visit the professor too, but Grandma said he's busy and I shouldn't bother him…"))
    closemessage
    end
}

script SandyshoreTown_EventScript_RuthNoticePlayer {
    lock
    applymovement(LOCALID_RUTH, Common_Movement_FacePlayer)
    waitmovement(0)
    applymovement(LOCALID_RUTH, Common_Movement_ExclamationMarkAndDelay)
    playbgm(MUS_ENCOUNTER_MAY, FALSE)
    playse(SE_PIN)
    waitmovement(0)
    speakername(CommonName_Ruth)
    msgbox(format("{PLAYER}! There you are!"))
    turnobject(LOCALID_RUTH, DIR_EAST)
    msgbox(format("Eevee, you'll have to go in your ball for now."))
    closemessage
    playmoncry(SPECIES_EEVEE, CRY_MODE_NORMAL)
    waitmoncry
    applymovement(LOCALID_EEVEE, EnterPokeballMovement)
    setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_ITEM_BALL)
    removeobject(LOCALID_EEVEE)
    addobject(LOCALID_EEVEE)
    delay(32)
    applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    removeobject(LOCALID_EEVEE)
    delay(16)
    applymovement(LOCALID_RUTH, Common_Movement_FacePlayer)
    waitmovement(0)
    speakername(CommonName_Ruth)
    msgbox(format("Let's go {PLAYER}, I bet the professor is already waiting for us!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkUp)
    waitmovement(0)
    opendoor(26, 8)
    applymovement(LOCALID_RUTH, Common_Movement_WalkUp)
	waitdooranim
    waitmovement(0)
    closedoor(26, 8)
    removeobject(LOCALID_RUTH)
    setflag(TEMP_FLAG_HIDE_RUTH)
    fadedefaultbgm
    waitdooranim
    setvar(VAR_LITTLEROOT_INTRO_STATE, 5)
    release
    end
}

script SandyshoreTown_EventScript_OnTransition {
    setflag(FLAG_VISITED_SANDYSHORE_TOWN)	
    if (var(VAR_LITTLEROOT_INTRO_STATE) == 4) {
        clearflag(TEMP_FLAG_HIDE_RUTH)
        clearflag(TEMP_FLAG_HIDE_EEVEE)
        setvar(VAR_OBJ_GFX_ID_0, OBJ_EVENT_GFX_SPECIES(EEVEE))
        removeobject(LOCALID_EEVEE)
        addobject(LOCALID_EEVEE)
    }
    else {
        setflag(TEMP_FLAG_HIDE_RUTH)
        setflag(TEMP_FLAG_HIDE_EEVEE)
    }
    if (!flag(FLAG_RECEIVED_STARTER)) {
        setvar(TEMP_VAR_PREVENT_NORTH_EXIT_TRIGGER_ACTIVE, 1)
        setobjectxyperm(LOCALID_LITTLE_GIRL, 21, 2)
        setobjectmovementtype(LOCALID_LITTLE_GIRL, MOVEMENT_TYPE_LOOK_AROUND)
    }
    else {
        setvar(TEMP_VAR_PREVENT_NORTH_EXIT_TRIGGER_ACTIVE, 0)
    }
}

text SandyshoreTown_Text_LittleGirlPreventExit {
    format("Ah, {PLAYER}!\pI saw Ruth in front of the Inn!\pI wanna go visit the professor too, but Grandma said he's busy…")
}

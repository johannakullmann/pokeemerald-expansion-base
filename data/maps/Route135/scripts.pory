const LOCALID_RUTH = 5
const LOCALID_ROCK_SMASH_GUY = 7
const LOCALID_ROCK_SMASH_ROCK = 3
const TEMP_FLAG_ROCK_DESTROYED = FLAG_TEMP_11
const TEMP_FLAG_HIDE_RUTH = FLAG_TEMP_10

mapscripts Route135_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_LITTLEROOT_INTRO_STATE) == 8) {
            clearflag(TEMP_FLAG_HIDE_RUTH)
        }
        else {
            setflag(TEMP_FLAG_HIDE_RUTH)
        }
    }
}

script Route135_EventScript_CatchTutorialEnterTop {
    lockall
    turnobject(LOCALID_RUTH, DIR_EAST)
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    speakername(CommonName_Ruth)
    msgbox(Route135_Text_CatchTutorialPsstComeOverHere)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route135_Movement_CatchTutorialEnterTop)
    waitmovement(0)
    delay(10)
    goto(Route135_EventScript_CatchTutorialMain)
}

script Route135_EventScript_CatchTutorialEnterBottom {
    lockall
    turnobject(LOCALID_RUTH, DIR_EAST)
    playbgm(MUS_ENCOUNTER_MAY, TRUE)
    speakername(CommonName_Ruth)
    msgbox(Route135_Text_CatchTutorialPsstComeOverHere)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route135_Movement_CatchTutorialEnterBottom)
    waitmovement(0)
    delay(10)
    goto(Route135_EventScript_CatchTutorialMain)
}

script Route135_EventScript_CatchTutorialMain {
    turnobject(LOCALID_RUTH, DIR_WEST)
    speakername(CommonName_Ruth)
    msgbox(format("See that Budew over there in the grass?\pWatch closely!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkLeft)
    waitmovement(0)
    special(SavePlayerParty)
	special(LoadCatchTutorialParty)
    special(StartWallyTutorialBattle)
    waitstate
    setvar(VAR_LITTLEROOT_INTRO_STATE, 9) 
    special(LoadPlayerParty)
    applymovement(LOCALID_RUTH, Common_Movement_WalkRight)
    waitmovement(0)
    speakername(CommonName_Ruth)
    msgbox(format("See? It's not hard at all!\pHere, you should try it yourself!"))
    speakername(NULL)
    giveitem(ITEM_POKE_BALL, 5)
    speakername(CommonName_Ruth)
    msgbox(format("The more you weaken a Pokémon, the easier it is to catch.\pInflicting a status condition will also increase your chances!\p"
            "Or you could defeat it to have your own Pokémon earn battle experience!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    speakername(CommonName_Ruth)
    msgbox(format("I'll go ahead to Pacifidlog Town, I need to to grab more Poké Balls.\p"
                "I wouldn't want to have to pass on a rare one because I ran out!"))
    applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    if (!flag(FLAG_SHOWED_STARTER_TO_MOM)) {
        msgbox(format("You should meet me there, and try to get the hang of battling on the way!\p"
                    "But before you go, why don't you show your new Pokémon to your mom?\p"
                    "I bet she'll love it!"))
    }
    else {
        msgbox(format("You should meet me there, and try to get the hang of battling on the way!"))
    }
    closemessage
    applymovement(LOCALID_RUTH, Route135_Movement_RuthLeave)
    savebgm(MUS_DUMMY)
    fadedefaultbgm
    waitmovement(0)
    setflag(TEMP_FLAG_HIDE_RUTH)
    removeobject(LOCALID_RUTH)
    setflag(FLAG_DONE_CATCH_TUTORIAL)
    releaseall
    end
}

script Route135_EventScript_RockSmashGuy {
    lock
    faceplayer
    speakername("Makoto")
    if (!flag(TEMP_FLAG_ROCK_DESTROYED)) {
        msgbox(format("Rocks like these are breakable! Watch!"))
        closemessage
        applymovement(LOCALID_ROCK_SMASH_GUY, Common_Movement_WalkInPlaceDown)
        waitmovement(0)
        delay(20)
        applymovement(LOCALID_ROCK_SMASH_ROCK, Movement_SmashRock)
        playse(SE_M_ROCK_THROW)
        speakername("Makoto")
        msgbox("HIYA!")
        waitmovement(0)
        waitse
        removeobject(LOCALID_ROCK_SMASH_ROCK)
        setflag(TEMP_FLAG_ROCK_DESTROYED)

        applymovement(LOCALID_ROCK_SMASH_GUY, Common_Movement_FacePlayer)
        waitmovement(0)
    }
    msgbox(format("If you steel your body and train your muscles, you can smash these rocks too!\p"
            "Or you can ask a strong Pokémon like Machop to break them for you, I guess."))
    release
    end
}

script Route135_EventScript_SmashedSpecialRock {
	lock
    closemessage
    if (!flag(FLAG_RECEIVED_HM_ROCK_SMASH)) {
        applymovement(LOCALID_ROCK_SMASH_GUY, Route135_Movement_RockSmashGuyNotice)
        playse(SE_PIN)
        waitmovement(LOCALID_ROCK_SMASH_GUY)
        specialvar(VAR_TEMP_1, GetPlayerFacingDirection)
        switch (var(VAR_TEMP_1)) {
            case DIR_SOUTH:
            case DIR_EAST:
                turnobject(LOCALID_ROCK_SMASH_GUY, DIR_WEST)
            case DIR_WEST:
                turnobject(LOCALID_ROCK_SMASH_GUY, DIR_EAST)
            case DIR_NORTH:
                turnobject(LOCALID_ROCK_SMASH_GUY, DIR_SOUTH)
        }
        speakername("Makoto")
        msgbox(format("That was a powerful Rock Smash! You trained your Pokémon well!\p"
                "Here, with this HM you can use it in battle too!\pOr you can teach it to other Pokémon that can't use it innately."))
        giveitem(ITEM_HM_ROCK_SMASH)
        setflag(FLAG_RECEIVED_HM_ROCK_SMASH)
        closemessage
    }
    releaseall
    end
}

text Route135_Text_CatchTutorialPsstComeOverHere {
    format("Psst! {PLAYER}!\pCome over here, quickly!")
}

movement Route135_Movement_CatchTutorialEnterTop {
    walk_left *3
    walk_up *2
    face_left
}

movement Route135_Movement_CatchTutorialEnterBottom {
    walk_left *3
    walk_up *3
    face_left
}

movement Route135_Movement_RuthLeave {
    walk_left *2
    walk_up *4
    walk_left *5
}

script Route135_EventScript_Boy1 {
    speakername("Alvin")
    msgbox(format("Did you know that Pokémon have different weaknesses depending on their type?\p"
        "My Wingull can easily defeat wild Budew with its flying-type moves!\p"
        "It doesn't do well against electric or rock-type attacks, though…"), MSGBOX_NPC)
}

script Route135_EventScript_Lia {
	trainerbattle_double(TRAINER_EMI_AND_LIA, 
            format("We'll battle you together!"), 
            format("Uh-oh, we lost."), 
            format("If you have two Pokémon, you can try to beat us!\pBut don't cry if you lose!"))
    speakername("Lia")
    msgbox (format("You have to think about all kinds of things when you're battling against two trainers."), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Ali{
	trainerbattle_double(TRAINER_EMI_AND_LIA, 
            format("We'll battle you together!"), 
            format("Aw."), 
            format("Oh, you only have one Pokémon! You can't battle us like that!"))
    speakername("Emi")
    msgbox (format("Double battles are much harder than single battles!"), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Darien {
	trainerbattle_single(TRAINER_DARIEN, 
            format("{PLAYER}! You have a Pokémon too now?\pLet me check if you are any good!"), 
            format("Aw, shucks."))
    speakername("Darien")
    msgbox (format("As a trainer, you have to be ready for a challenge anytime!"), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Lass {
    lock
    faceplayer
    speakername("Kirstin")
    msgbox (format("I jumped down this ledge, but now I can't get back up!\p"
            "I could get back to Pacifidlog via the beach, but I don't want to get sand in my shoes…"), MSGBOX_AUTOCLOSE)
    end
}

script Route135_EventScript_Tuber_F {
    lock
    faceplayer
    speakername("Ida")
    msgbox (format("Hihi!\pMy footsteps make funny sounds!"), MSGBOX_AUTOCLOSE)
    end
}

movement Route135_Movement_RockSmashGuyNotice {
    face_down
	emote_exclamation_mark
	delay_16
	delay_16
	delay_16
	delay_16
    walk_down
}

movement Route135_Movement_RockSmashRockSmash {
    delay_16
    delay_16
    delay_16
    rock_smash_break
}


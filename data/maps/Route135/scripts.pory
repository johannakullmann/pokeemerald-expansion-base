const LOCALID_RUTH = 5
const LOCALID_ROCK_SMASH_GUY = 7
const LOCALID_ROCK_SMASH_ROCK = 3
const TEMP_FLAG_ROCK_DESTROYED = FLAG_TEMP_11
const TEMP_FLAG_HIDE_RUTH = FLAG_TEMP_10

mapscripts Route135_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        if (var(VAR_LITTLEROOT_INTRO_STATE) == 8) {
            clearflag(TEMP_FLAG_HIDE_RUTH)
        }
        else {
            setflag(TEMP_FLAG_HIDE_RUTH)
        }
    }
}

script Route135_EventScript_CatchTutorialEnterTop {
    lockall
    turnobject(LOCALID_RUTH, DIR_EAST)
    msgbox(Route135_Text_CatchTutorialPsstComeOverHere)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route135_Movement_CatchTutorialEnterTop)
    waitmovement(0)
    goto(Route135_EventScript_CatchTutorialMain)
}

script Route135_EventScript_CatchTutorialEnterBottom {
    lockall
    turnobject(LOCALID_RUTH, DIR_EAST)
    msgbox(Route135_Text_CatchTutorialPsstComeOverHere)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Route135_Movement_CatchTutorialEnterBottom)
    waitmovement(0)
    delay(10)
    goto(Route135_EventScript_CatchTutorialMain)
}

script Route135_EventScript_CatchTutorialMain {
    turnobject(LOCALID_RUTH, DIR_WEST)
    msgbox(format("See that Budew over there in the grass?\pWatch closely!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkLeft)
    waitmovement(0)
    special(SavePlayerParty)
	special(LoadCatchTutorialParty)
    special(StartWallyTutorialBattle)
    waitstate
    setvar(VAR_LITTLEROOT_INTRO_STATE, 9) 
    special(LoadPlayerParty)
    applymovement(LOCALID_RUTH, Common_Movement_WalkRight)
    waitmovement(0)
    msgbox(format("See? It's not hard at all!\pHere, you should try it yourself!"))
    closemessage
    giveitem(ITEM_POKE_BALL, 3)
    msgbox(format("The more you weaken a Pokémon, the easier it is to catch.\pInflicting a status condition will also increase your chances!\p"
            "If you don't want to catch a Pokémon, you can defeat it instead.\pThat way your own Pokémon can earn battle experience!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceLeft)
    waitmovement(0)
    msgbox(format("Since I gave my three last balls to you, I'll go to the mart in Pacifidlog Town and buy new ones.\p"
                "I wouldn't want to have to pass on a rare Pokémon just because I forgot to buy Poké Balls!"))
    closemessage
    applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceRight)
    waitmovement(0)
    msgbox(format("You should meet me there, and try to get the hang of battling on the way!\p"
                "But before you go, why don't you show your new Pokémon to your mom?\p"
                "I bet she'll love it!"))
    closemessage
    applymovement(LOCALID_RUTH, Route135_Movement_RuthLeave)
    waitmovement(0)
    setflag(TEMP_FLAG_HIDE_RUTH)
    removeobject(LOCALID_RUTH)
    setflag(FLAG_DONE_CATCH_TUTORIAL)
    releaseall
    end
}

script Route135_EventScript_RockSmashGuy {
    lock
    faceplayer
    if (!flag(TEMP_FLAG_ROCK_DESTROYED)) {
    msgbox(format("Rocks like these are breakable! Watch!"))
    applymovement(LOCALID_ROCK_SMASH_GUY, Common_Movement_WalkInPlaceDown)
    waitmovement(0)
    applymovement(LOCALID_ROCK_SMASH_ROCK, Movement_SmashRock)
    playse(SE_M_ROCK_THROW)
    msgbox("HIYA!")
    waitmovement(0)
    waitse
	removeobject(LOCALID_ROCK_SMASH_ROCK)
    setflag(TEMP_FLAG_ROCK_DESTROYED)

    specialvar(VAR_TEMP_1, GetPlayerFacingDirection)
    switch (var(VAR_TEMP_1)) {
        case DIR_SOUTH:
        case DIR_EAST:
            turnobject(LOCALID_ROCK_SMASH_GUY, DIR_WEST)
        case DIR_WEST:
			turnobject(LOCALID_ROCK_SMASH_GUY, DIR_EAST)
        case DIR_NORTH:
			turnobject(LOCALID_ROCK_SMASH_GUY, DIR_SOUTH)
    }
    }
    msgbox(format("If you steel your body and train your muscles, you can smash these rocks too!\p"
            "Or you can ask a strong Pokémon to break them for you, I guess."))
    release
    end
}

script Route135_EventScript_RockSmashSpecialRock {
	lock
    if (flag(FLAG_RECEIVED_HM_ROCK_SMASH)) {
        goto(EventScript_RockSmash)
    }
	checkpartymove(MOVE_ROCK_SMASH)
	if (var(VAR_RESULT) == PARTY_SIZE) {
        goto(EventScript_CantSmashRock)
    } 
	setfieldeffectargument(0, VAR_RESULT)
	bufferpartymonnick(STR_VAR_1, VAR_RESULT)
	buffermovename(STR_VAR_2, MOVE_ROCK_SMASH)
	msgbox(Text_WantToSmash, MSGBOX_YESNO)
	if(var(VAR_RESULT) == NO)
    {
        goto(EventScript_CancelSmash)
    }

	msgbox(Text_MonUsedFieldMove, MSGBOX_DEFAULT)
	closemessage
	dofieldeffect(FLDEFF_USE_ROCK_SMASH)
	waitstate
	applymovement(LOCALID_ROCK_SMASH_ROCK, Movement_SmashRock)
	waitmovement(0)
	removeobject(LOCALID_ROCK_SMASH_ROCK)
    setflag(TEMP_FLAG_ROCK_DESTROYED)

    applymovement(LOCALID_ROCK_SMASH_GUY, Route135_Movement_RockSmashGuyNotice)
    playse(SE_PIN)
    waitmovement(LOCALID_ROCK_SMASH_GUY)
    specialvar(VAR_TEMP_1, GetPlayerFacingDirection)
	switch (var(VAR_TEMP_1)) {
        case DIR_SOUTH:
        case DIR_EAST:
            turnobject(LOCALID_ROCK_SMASH_GUY, DIR_WEST)
        case DIR_WEST:
			turnobject(LOCALID_ROCK_SMASH_GUY, DIR_EAST)
        case DIR_NORTH:
			turnobject(LOCALID_ROCK_SMASH_GUY, DIR_SOUTH)
    }
    checkpartymove(MOVE_ROCK_SMASH)
	bufferpartymonnick(STR_VAR_1, VAR_RESULT)
    msgbox(format("That's a powerful Rock Smash! You trained your {STR_VAR_1} well!\p"
            "Here, with this HM you can use it in battle too!\pOr you can teach it to other Pokémon that can't use it innately."))
    giveitem(ITEM_HM_ROCK_SMASH)
    setflag(FLAG_RECEIVED_HM_ROCK_SMASH)
    closemessage
    releaseall
	end
}

text Route135_Text_CatchTutorialPsstComeOverHere {
    format("Psst! {PLAYER}!\pCome over here, quickly!")
}

movement Route135_Movement_CatchTutorialEnterTop {
    walk_left *3
    walk_up *2
    face_left
}

movement Route135_Movement_CatchTutorialEnterBottom {
    walk_left *3
    walk_up *3
    face_left
}

movement Route135_Movement_RuthLeave {
    walk_left *2
    walk_up *4
    walk_left *3
}

script Route135_EventScript_Boy1 {
    msgbox(format("Did you know that Pokémon have different weaknesses depending on their type?\p"
        "My Wingull can easily defeat wild Budew with its flying-type moves!\p"
        "It doesn't do well against eletric or rock-type attacks, though…"), MSGBOX_NPC)
}

script Route135_EventScript_Lia {
	trainerbattle_double(TRAINER_ALI_AND_LIA, 
            format("Lia: I'll battle you together with my sister!"), 
            format("Lia: Uh-oh, we lost."), 
            format("Lia: If you have two Pokémon, you can try to beat us!\pBut don't cry if you lose!"))
    msgbox (format("Lia: You have to think about all kinds of things when you're battling against two trainers."), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Ali{
	trainerbattle_double(TRAINER_ALI_AND_LIA, 
            format("Ali: Let's go, Lia!"), 
            format("Ali: Aw."), 
            format("Ali: Oh, you only have one Pokémon! You can't battle us like that!"))
    msgbox (format("Ali: Double battles are much harder than single battles!"), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Darien {
	trainerbattle_single(TRAINER_DARIEN, 
            format("{PLAYER}! You have a Pokémon too now?\pLet me check if you are any good!"), 
            format("Aw, shucks."))
    msgbox (format("As a trainer, you have to be ready for a challenge anytime!"), MSGBOX_AUTOCLOSE)
	end
}

script Route135_EventScript_Lass {
    lock
    faceplayer
    msgbox (format("I jumped down this ledge, but now I can't get back up!\p"
            "I could get back to Pacifidlog via the beach, but I don't want to get sand in my shoes…"), MSGBOX_AUTOCLOSE)
    end
}

script Route135_EventScript_Tuber_F {
    lock
    faceplayer
    msgbox (format("Hehe!\pMy footsteps make funny sounds!"), MSGBOX_AUTOCLOSE)
    end
}

movement Route135_Movement_RockSmashGuyNotice {
    face_down
	emote_exclamation_mark
	delay_16
	delay_16
	delay_16
	delay_16
    walk_down
}

movement Route135_Movement_RockSmashRockSmash {
    delay_16
    delay_16
    delay_16
    rock_smash_break
}


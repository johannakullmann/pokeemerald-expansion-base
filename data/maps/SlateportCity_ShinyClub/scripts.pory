mapscripts SlateportCity_ShinyClub_MapScripts {

}

script SlateportCity_ShinyClub_EventScript_BugCatcher {
    msgbox(format("I love Wurmple!\pI even found a purple one, look! Its name is Purmple!\p"
    "I can't wait to see if it'll evolve into Beautifly or Dustox!"), MSGBOX_NPC)
    end
}

script SlateportCity_ShinyClub_EventScript_Hiker {
    msgbox(format("I found this little nugget while hiking in the mountains!\pIt has been with me ever since."), MSGBOX_NPC)
    end
}

script SlateportCity_ShinyClub_EventScript_ShinyBook {
    msgbox(format("Shiny Pokémon:\p"
        "Very rarely, one can find Pokémon that differ in color from their usual coloration.\p"
        "While they are not stronger than their normal-colored counterparts, "
        "they are very sought after by collectors.\p"
        "Finding one in the wild is said to be a sign of good luck."), MSGBOX_NPC)
    end
}

script SlateportCity_ShinyClub_EventScript_ShinyClubChairman {
    checkitem(ITEM_CLUB_MEMBER_CARD) 
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("Welcome back, {PLAYER}!\pHow have you been?"))
    }
    else {
        special(IsShinyMonInParty)
        if (var(VAR_RESULT) < PARTY_SIZE) {
            copyvar(VAR_0x8004, VAR_RESULT)
            specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)
            bufferspeciesname(STR_VAR_1, VAR_RESULT)
            msgbox(format("I'm the chairman of the Shiny Pokémon Club!\p"
            "Here, we're big fans of rare odd-colored Pokémon and show them off to each other.\p"
            "What about you? Do you have a shiny Pokémon?\p"
            "... Oh? Your {STR_VAR_1}...\p"
            "What a beauty! You're so lucky to have found such a rare Pokémon!\p"
            "Are you interested in joining our little club? Of course you are!\p"
            "Here, take this membership card!"), MSGBOX_NPC)
            giveitem(ITEM_CLUB_MEMBER_CARD)
            msgbox(format("I hope you stop by often!"), MSGBOX_NPC)
        }
        else {
            msgbox(format("I'm the chairman of the Shiny Pokémon Club!\p"
            "Here, we're big fans of rare odd-colored Pokémon and show them off to each other.\p"
            "What about you? Do you have a shiny Pokémon?\p"
            "If you find one, make sure to come back and show it to me!"), MSGBOX_NPC)
        }
    }
    end
}

script SlateportCity_ShinyClub_EventScript_Cooltrainer {
    lock
    faceplayer
    if (defeated(TRAINER_SHINY_COOLTRAINER)) {
        msgbox(format("Finding a surprise shiny on your journey makes you use Pokémon you otherwise wouldn't have."))
    }
    else {
    msgbox(format("Do you want to see my shiny Pokémon?\pThey are not only beautiful, but also strong!"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
	        trainerbattle_no_intro(TRAINER_SHINY_COOLTRAINER, format("Wow."))
        }
    }
    release 
	end
}

script Route135_ShinyHouse_EventScript_ShinyLureSeller {
    checkitem(ITEM_CLUB_MEMBER_CARD) 
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("I invented a special lure that attracts odd-colored Pokémon! Want to buy some?"), MSGBOX_NPC)
        waitmessage
        pokemart(ShinyLureMart)
        lock
        faceplayer
        msgbox("Good luck on your hunt!")
    }
    else {
        msgbox(format("I invented a special lure that attracts odd-colored Pokémon!\p"
                "They're hard to make, so I only sell them to members for now."), MSGBOX_NPC)
    }
        release
        end
}

script SlateportCity_ShinyClub_EventScript_Geodude {
    playmoncry(SPECIES_GEODUDE, CRY_MODE_NORMAL)
    msgbox(format("Geodude: Gyuuu!"), MSGBOX_NPC)
    end
}

script SlateportCity_ShinyClub_EventScript_Wurmple {
    playmoncry(SPECIES_WURMPLE, CRY_MODE_NORMAL)
    msgbox(format("Wurmple: Rrrrz!"), MSGBOX_NPC)
    end
}

mart ShinyLureMart {
    ITEM_SHINY_LURE
    ITEM_SUPER_SHINY_LURE
    ITEM_MAX_SHINY_LURE
}
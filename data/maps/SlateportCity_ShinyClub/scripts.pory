mapscripts SlateportCity_ShinyClub_MapScripts {

}

script SlateportCity_ShinyClub_EventScript_BugCatcher {
    speakername("Tim")
    msgbox(format("I love Wurmple!\pI even found a purple one, look! Its name is Purmple!\p"
    "I can't wait to see if it'll evolve into Beautifly or Dustox!"), MSGBOX_NPC)
    closemessage
    end
}

script SlateportCity_ShinyClub_EventScript_Hiker {
    speakername("Frederic")
    msgbox(format("I found this little nugget while hiking in the mountains!\pIt has been with me ever since."), MSGBOX_NPC)
    closemessage
    end
}

script SlateportCity_ShinyClub_EventScript_ShinyBook {
    msgbox(format("Shiny Pokémon:\p"
        "Very rarely, one can find Pokémon that differ in color from their usual coloration.\p"
        "While they are not stronger than their normal-colored counterparts, "
        "they are very sought after by collectors.\p"
        "Finding one in the wild is said to be a sign of good luck."), MSGBOX_SIGN)
    end
}

script SlateportCity_ShinyClub_EventScript_ShinyClubChairman {
    lock
    faceplayer
    speakername(CommonName_ShinyChairman)
    checkitem(ITEM_CLUB_MEMBER_CARD) 
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("Welcome back, {PLAYER}!\pHow have you been?"))
    }
    else {
        special(IsShinyMonInParty)
        if (var(VAR_RESULT) < PARTY_SIZE) {
            copyvar(VAR_0x8004, VAR_RESULT)
            specialvar(VAR_RESULT, ScriptGetPartyMonSpecies)
            bufferspeciesname(STR_VAR_1, VAR_RESULT)
            msgbox(format("I'm the chairman of the Shiny Pokémon Club!\p"
            "Here, we're big fans of rare odd-colored Pokémon and show them off to each other.\p"
            "What about you? Do you have a shiny Pokémon?\p"
            "… Oh? Your {STR_VAR_1}…\p"
            "What a beauty! You're so lucky to have found such a rare Pokémon!\p"
            "Are you interested in joining our little club? Of course you are!\p"
            "Here, take this membership card!"))
            speakername(NULL)
            giveitem(ITEM_CLUB_MEMBER_CARD)
            speakername(CommonName_ShinyChairman)
            msgbox(format("I hope you stop by often!"))
        }
        else {
            msgbox(format("I'm the chairman of the Shiny Pokémon Club!\p"
            "Here, we're big fans of rare odd-colored Pokémon and show them off to each other.\p"
            "What about you? Do you have a shiny Pokémon?\p"
            "If you find one, make sure to come back and show it to me!"))
        }
    }
    closemessage
    end
}

script SlateportCity_ShinyClub_EventScript_Cooltrainer {
    lock
    faceplayer
    speakername("Luma")
    if (defeated(TRAINER_SHINY_COOLTRAINER)) {
        msgbox(format("Finding a surprise shiny on your journey makes you use Pokémon you otherwise wouldn't have."))
    }
    else {
    msgbox(format("Do you want to see my shiny Pokémon?\pThey are not only beautiful, but also strong!"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == YES) {
	        trainerbattle_no_intro(TRAINER_SHINY_COOLTRAINER, format("Wow."))
        }
    }
    release
	end
}

script Route135_ShinyHouse_EventScript_ShinyLureSeller {
    lock
    faceplayer
    speakername("Rupert")
    checkitem(ITEM_CLUB_MEMBER_CARD) 
    if (var(VAR_RESULT) == TRUE) {
        msgbox(format("I invented a special lure that attracts odd-colored Pokémon! Want to buy some?"))
        waitmessage
        lock
        pokemart(ShinyLureMart)
        msgbox("Good luck on your hunt!")
    }
    else {
        msgbox(format("I invented a special lure that attracts odd-colored Pokémon!\p"
                "They're hard to make, so I only sell them to members for now."))
    }
    closemessage
    release
    end
}

script SlateportCity_ShinyClub_EventScript_Geodude {
    speakername(CommonName_Geodude)
    playmoncry(SPECIES_GEODUDE, CRY_MODE_NORMAL)
    msgbox(format("Gyuuu!"), MSGBOX_NPC)
    closemessage
    end
}

script SlateportCity_ShinyClub_EventScript_Wurmple {
    speakername(CommonName_Wurmple)
    playmoncry(SPECIES_WURMPLE, CRY_MODE_NORMAL)
    msgbox(format("Rrrrz!"), MSGBOX_NPC)
    closemessage
    end
}

script SlateportCity_ShinyClub_EventScript_Purmple {
    speakername("Purmple")
    playmoncry(SPECIES_WURMPLE, CRY_MODE_NORMAL)
    msgbox(format("Rrrrz!"), MSGBOX_NPC)
    closemessage
    end
}

mart ShinyLureMart {
    ITEM_SHINY_LURE
    ITEM_SUPER_SHINY_LURE
    ITEM_MAX_SHINY_LURE
}
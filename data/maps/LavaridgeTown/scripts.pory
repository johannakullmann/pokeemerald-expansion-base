raw `
.set LOCALID_RIVAL_ON_BIKE, 7
.set LOCALID_RIVAL, 8

LavaridgeTown_EventScript_ClearLavaridgeWhiteOut::
	clearflag FLAG_WHITEOUT_TO_LAVARIDGE
	return

LavaridgeTown_EventScript_ShowMtChimneyTrainers::
	clearflag FLAG_HIDE_MT_CHIMNEY_TRAINERS
	return

LavaridgeTown_EventScript_HideMapNamePopup::
	setflag FLAG_HIDE_MAP_NAME_POPUP
	return

LavaridgeTown_EventScript_HotSpringsTrigger::
	specialvar VAR_RESULT, GetPlayerFacingDirection
	goto_if_eq VAR_RESULT, DIR_SOUTH, LavaridgeTown_EventScript_EnteredHotSprings
	end

LavaridgeTown_EventScript_EnteredHotSprings::
	incrementgamestat GAME_STAT_ENTERED_HOT_SPRINGS
	end

LavaridgeTown_EventScript_ExpertM::
	msgbox LavaridgeTown_Text_HotSpringsNeverRunDry, MSGBOX_NPC
	end

LavaridgeTown_EventScript_OldMan::
	msgbox LavaridgeTown_Text_PokemonNippedBackside, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_Twin::
	msgbox LavaridgeTown_Text_BatheInHotSpringsEveryDay, MSGBOX_NPC
	end

LavaridgeTown_EventScript_HotSpringsOldWoman1::
	msgbox LavaridgeTown_Text_IfPokemonInHotSprings, MSGBOX_NPC
	end

LavaridgeTown_EventScript_HotSpringsOldWoman2::
	msgbox LavaridgeTown_Text_HotSpringsClaims, MSGBOX_NPC
	end

LavaridgeTown_EventScript_ExpertF::
	msgbox LavaridgeTown_Text_OhYouLikeHotSprings, MSGBOX_NPC
	end

LavaridgeTown_EventScript_EggWoman::
	lock
	faceplayer
	goto_if_set FLAG_RECEIVED_LAVARIDGE_EGG, LavaridgeTown_EventScript_ReceivedEgg
	msgbox LavaridgeTown_Text_HaveEggWillYouTakeIt, MSGBOX_YESNO
	goto_if_eq VAR_RESULT, NO, LavaridgeTown_EventScript_DeclineEgg
	getpartysize
	goto_if_eq VAR_RESULT, PARTY_SIZE, LavaridgeTown_EventScript_NoRoomForEgg
	msgbox LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg, MSGBOX_DEFAULT
	setflag FLAG_RECEIVED_LAVARIDGE_EGG
	playfanfare MUS_OBTAIN_ITEM
	message LavaridgeTown_Text_ReceivedTheEgg
	waitfanfare
	giveegg SPECIES_WYNAUT
	release
	end

LavaridgeTown_EventScript_ReceivedEgg::
	msgbox LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_NoRoomForEgg::
	msgbox LavaridgeTown_Text_NoRoomForThisEgg, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_DeclineEgg::
	msgbox LavaridgeTown_Text_AsYouWishThen, MSGBOX_DEFAULT
	release
	end

LavaridgeTown_EventScript_TownSign::
	msgbox LavaridgeTown_Text_TownSign, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_GymSign::
	msgbox LavaridgeTown_Text_GymSign, MSGBOX_SIGN
	end

LavaridgeTown_EventScript_HerbShopSign::
	msgbox LavaridgeTown_Text_HerbShopSign, MSGBOX_SIGN
	end

LavaridgeTown_Text_HaveEggWillYouTakeIt:
	.string "I have here an EGG.\p"
	.string "I'd hoped to hatch it by covering it in\n"
	.string "hot sand by the hot springs.\l"
	.string "But that doesn't seem to be enough…\p"
	.string "I've heard it would be best if it were\n"
	.string "kept together with POKéMON and\l"
	.string "carried about.\p"
	.string "You are a TRAINER, yes?\n"
	.string "And your POKéMON radiate vitality.\p"
	.string "So, what say you?\n"
	.string "Will you take this EGG to hatch?$"

LavaridgeTown_Text_HopeYoullWalkPlentyWithEgg:
	.string "Good! I hope you'll walk plenty with\n"
	.string "this here EGG!$"

LavaridgeTown_Text_ReceivedTheEgg:
	.string "{PLAYER} received the EGG.$"

LavaridgeTown_Text_NoRoomForThisEgg:
	.string "Oh? You've too many POKéMON.\n"
	.string "There's no room for this EGG…$"

LavaridgeTown_Text_AsYouWishThen:
	.string "As you wish, then…\p"
	.string "If you have a change of heart about\n"
	.string "hatching this EGG, I will be here.$"

LavaridgeTown_Text_EverySoOftenEggFoundAtDayCare:
	.string "Every so often, an EGG will be found at\n"
	.string "the POKéMON DAY CARE.\p"
	.string "Or at least that's how the rumor goes.$"

LavaridgeTown_Text_HotSpringsNeverRunDry:
	.string "We draw as much hot water as we need,\n"
	.string "and yet the hot springs never run dry.\p"
	.string "Isn't it magical?\p"
	.string "These hot springs appear near active\n"
	.string "volcanoes. Veins of water under the\l"
	.string "ground are heated by magma to well up\l"
	.string "as hot springs.$"

LavaridgeTown_Text_PokemonNippedBackside:
	.string "Being buried in this hot sand is…\n"
	.string "Sigh…\p"
	.string "So warm and heavenly…\p"
	.string "Eh? Gyaah! Ouch!\p"
	.string "A POKéMON nipped my backside!$"

LavaridgeTown_Text_OhYouLikeHotSprings:
	.string "Oh, you like hot springs, do you?\p"
	.string "That's surprising for one as young\n"
	.string "as you.$"

LavaridgeTown_Text_BatheInHotSpringsEveryDay:
	.string "I bathe in the hot springs every day.\p"
	.string "I want to become a beautiful and strong\n"
	.string "GYM LEADER like FLANNERY.$"

LavaridgeTown_Text_IfPokemonInHotSprings:
	.string "If people put POKéMON in hot springs,\n"
	.string "it might be seriously strange.\p"
	.string "Why, it might be an electric bath, or\n"
	.string "a bubble bath, or even a lava bath…$"

LavaridgeTown_Text_HotSpringsClaims:
	.string "They're claiming that these hot springs\n"
	.string "are good for calming nervous tension,\l"
	.string "relieving aching muscles, solving\l"
	.string "romantic problems, and attracting\l"
	.string "money…$"

LavaridgeTown_Text_TownSign:
	.string "LAVARIDGE TOWN\p"
	.string "“POKéMON CENTER HOT SPRINGS\n"
	.string "An excellent place for relaxing!”$"

LavaridgeTown_Text_GymSign:
	.string "LAVARIDGE TOWN POKéMON GYM\n"
	.string "LEADER: May\l"
	.string "“One with a fiery passion that burns!”$"

LavaridgeTown_Text_HerbShopSign:
	.string "POKéMON HERB SHOP\n"
	.string "“Bitter taste--better cure!”$"
`
const LOCALID_FLANNERY = 8
const LOCALID_RUTH = 9
const TEMP_FLAG_HIDE_FLANNERY = FLAG_TEMP_10

mapscripts LavaridgeTown_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: LavaridgeTown_OnTransition
	MAP_SCRIPT_ON_LOAD: LavaridgeTown_OnLoad
}


script LavaridgeTown_OnTransition {
	setflag(FLAG_VISITED_LAVARIDGE_TOWN)
	if (flag(FLAG_WHITEOUT_TO_LAVARIDGE)) {
		call(LavaridgeTown_EventScript_ClearLavaridgeWhiteOut)
	}
	if (flag(FLAG_DEFEATED_EVIL_TEAM_MT_CHIMNEY)) {
		call (LavaridgeTown_EventScript_ShowMtChimneyTrainers)
	}
	setflag(TEMP_FLAG_HIDE_FLANNERY)
	end
}

script LavaridgeTown_OnLoad {
	if (!flag(FLAG_LAVARIDGE_GYM_OPEN)) {
		call (LavaridgeTown_EventScript_LockGymDoor)
	}
	return
}

script LavaridgeTown_EventScript_LockGymDoor {
	setmetatile(5, 15, METATILE_Lavaridge_GymDoor_Closed, TRUE)
	end
}

script EventScript_ClosedLavaridgeGymDoor {
	lock
	msgbox("The door is closed.", MSGBOX_SIGN)
	closemessage
	if (!flag(FLAG_MET_RUTH_LAVARIDGE)) {
		goto(LavaridgeTown_EventScript_Ruth)
	}
	end
}

script LavaridgeTown_EventScript_Ruth {
	lock
	faceplayer
	speakername(CommonName_Ruth)
	if (!flag(FLAG_MET_RUTH_LAVARIDGE)) {
		msgbox(format("Hey, {PLAYER}…\pLooks like we can't challenge the Fire-type gym yet, the door's closed."))
		closemessage
		clearflag(TEMP_FLAG_HIDE_FLANNERY)
		addobject(LOCALID_FLANNERY)
		showobjectat(LOCALID_FLANNERY, MAP_LAVARIDGE_TOWN)
		goto(LavaridgeTown_EventScript_FlanneryGymLeaderGoneScene)
	}
	elif (!flag(FLAG_DEFEATED_RIVAL_2)) {
		msgbox(format("The gym leader hasn't come back yet… How about we have a go at it again?"), MSGBOX_YESNO)
		if (var(VAR_RESULT) == YES) {
			goto(LavaridgeTown_EventScript_RuthBattle)
		}
	}
	release
	end
}

script LavaridgeTown_EventScript_FlanneryGymLeaderGoneScene {
	applymovement(LOCALID_FLANNERY, LavaridgeTown_Movement_FlanneryApproachGym)
	waitmovement(0)
	applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthNoticeFlannery)
	playse(SE_PIN)
	waitmovement(0)
	speakername(CommonName_Ruth)
	msgbox(format("Flannery?!"))
	closemessage
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	getplayerxy(VAR_0x8008, VAR_0x8009)
	if (var(VAR_0x8008) == 5) {
		applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthApproachFlannery_1)
	}
	else { 
		applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthApproachFlannery_2)
	}
	waitmovement(0)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	speakername(CommonName_Ruth)
	msgbox(format("What are you doing here?\pAren't you really busy with your new job?"))
	closemessage
	applymovement(LOCALID_FLANNERY, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	speakername(CommonName_Flannery)
	msgbox(format("Haha, the new gym leader asked me to help her out with some ruffians.\p"
		"But! Ruth! Auntie told me you're collecting badges now?\p"
		"Haha, to think last year you would've had to face me here!\p"
		"I would've grilled you!"))
	closemessage
	applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceRight)
	waitmovement(0)
	speakername(CommonName_Ruth)
	msgbox(format("Psh! I'll put out that fire of yours some day!"))
	applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceLeft)
	msgbox(format("Ah, right! {PLAYER}!"))
	closemessage
	if (var(VAR_0x8008) == 5) {
		applymovement(OBJ_EVENT_ID_PLAYER, LavaridgeTown_Movement_PlayerApproachFlannery_1)
	}
	elif (var(VAR_0x8008) == 4) {
		applymovement(OBJ_EVENT_ID_PLAYER, LavaridgeTown_Movement_PlayerApproachFlannery_2)
	}
	else {
		applymovement(OBJ_EVENT_ID_PLAYER, LavaridgeTown_Movement_PlayerApproachFlannery_3)
	}
	waitmovement(0)
	turnobject(LOCALID_RUTH, DIR_SOUTH)
	speakername(CommonName_Ruth)
	msgbox(format("You remember my cousin Flannery, right?\pShe was the gym leader of Lavaridge Town until recently."))
	closemessage
	applymovement(LOCALID_FLANNERY, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	turnobject(LOCALID_RUTH, DIR_EAST)
	speakername(CommonName_Flannery)
	msgbox(format("Ah, {PLAYER}! You're Ruth's neighbour, right?"))
	closemessage
	applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceRight)
	speakername(CommonName_Ruth)
	msgbox(format("Yes! {PLAYER} is also collecting badges! We're competing!\p"
		"That reminds me, do you know where the new gym leader is? The door is locked."))
	closemessage
	applymovement(LOCALID_FLANNERY, Common_Movement_WalkInPlaceLeft)
	waitmovement(0)
	speakername(CommonName_Flannery)
	msgbox(format("Huh? She's not inside?"))
	turnobject(LOCALID_FLANNERY, DIR_EAST)
	msgbox(format("That means she must've already left for Mt. Chimney!"))
	closemessage
	applymovement(LOCALID_FLANNERY, LavaridgeTown_Movement_FlanneryLeave_1)
	waitmovement(0)
	speakername(CommonName_Ruth)
	msgbox(format("Oh, yes. {PLAYER}, Ruth… Why don't you two check the hot springs until we got that thing sorted?"), MSGBOX_AUTOCLOSE)
	applymovement(LOCALID_FLANNERY, LavaridgeTown_Movement_FlanneryLeave_2)
	waitmovement(0)
	setflag(TEMP_FLAG_HIDE_FLANNERY)
	removeobject(LOCALID_FLANNERY)
	setflag(FLAG_MET_RUTH_LAVARIDGE)
	delay(30)
	speakername(CommonName_Ruth)
	msgbox(format("There she goes…"))
	applymovement(LOCALID_RUTH, Common_Movement_WalkInPlaceDown)
	waitmovement(0)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
	msgbox(format("… I don't feel like lazing around in the hot springs.\p{PLAYER}, care for a quick battle to pass the time?"))
	goto(LavaridgeTown_EventScript_RuthBattle)
	release
	end
}

script LavaridgeTown_EventScript_RuthBattle {
	switch (var(VAR_STARTER_MON)) {
			case 0:
				trainerbattle_no_intro(TRAINER_RUTH_LAVARIDGE_TOXATIEL, LavaridgeTown_Text_RuthDefeat)
			case 1:
				trainerbattle_no_intro(TRAINER_RUTH_LAVARIDGE_GROCKO, LavaridgeTown_Text_RuthDefeat)
			case 2:
				trainerbattle_no_intro(TRAINER_RUTH_LAVARIDGE_MERAKID, LavaridgeTown_Text_RuthDefeat)
	}	
	goto(LavaridgeTown_EventScript_AfterRuthBattle)
}

script LavaridgeTown_EventScript_AfterRuthBattle {
	lock
	speakername(CommonName_Ruth)
	msgbox(format("Ah, I thought I could beat you this time.\p"
		"Looks like I need to step up my game!\p"
		"My Barboach was falling off, I might have to go back to training…\p"
		"…\p"
		"I don't feel like waiting until they're done. {PLAYER}, let's go look for them!\p"
		"Flannery said they're at Mt. Chimney, right?"), MSGBOX_AUTOCLOSE)
	getplayerxy(VAR_0x8008, VAR_0x8009)
	if (var(VAR_0x8008) == 7 && var(VAR_0x8009) == 17) {
		applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthLeave_3)
	}
	if (var(VAR_0x8008) == 5 && var(VAR_0x8009) == 16) {
		applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthLeave_1)
	}
	else {
		applymovement(LOCALID_RUTH, LavaridgeTown_Movement_RuthLeave_2)
	}
	waitmovement(0)
	removeobject(LOCALID_RUTH)
	setflag(FLAG_DEFEATED_RIVAL_2)
	setvar(VAR_JAGGED_PASS_STATE, 1)
	setflag(FLAG_DEFEATED_RIVAL_2)
	setflag(FLAG_HIDE_JAGGED_PASS_NPCS)
	release
	end
}

movement LavaridgeTown_Movement_FlanneryApproachGym {
	walk_down * 6
	walk_left
}

movement LavaridgeTown_Movement_FlanneryApproachPlayer {
	walk_left *2
}

movement LavaridgeTown_Movement_FlanneryLeave_1 {
	walk_fast_right *1
	walk_fast_up
	walk_up
	delay_16
	delay_16
	walk_down
}

movement LavaridgeTown_Movement_FlanneryLeave_2 {
	walk_fast_up*4
}

movement LavaridgeTown_Movement_RuthNoticeFlannery {
	walk_in_place_fast_right
	emote_exclamation_mark
	delay_16*4
}

movement LavaridgeTown_Movement_RuthApproachFlannery_1 {
	walk_fast_down
	walk_fast_right*2
	walk_fast_up
	walk_fast_right
}

movement LavaridgeTown_Movement_RuthApproachFlannery_2 {
	walk_fast_right *3
}

movement LavaridgeTown_Movement_PlayerApproachFlannery_1 {
	walk_down
	walk_right *2
	face_up
}

movement LavaridgeTown_Movement_PlayerApproachFlannery_2 {
	walk_right *3
	face_up
}

movement LavaridgeTown_Movement_PlayerApproachFlannery_3 {
	walk_down
	walk_right*4
	face_up
}

movement LavaridgeTown_Movement_RuthLeave_1 {
	walk_down
	walk_right *5
	walk_up *7
}

movement LavaridgeTown_Movement_RuthLeave_2 {
	walk_right *5
	walk_up *6
}

movement LavaridgeTown_Movement_RuthLeave_3 {
	walk_right *2
	walk_up *6
}

text LavaridgeTown_Text_RuthDefeat {
	format("Darn, I thought I could win this one.")
}